let window = 1h;
let PwdWriteBackAppRoles = dynamic(['PasswordWriteback.RefreshClient.All','PasswordWriteback.RegisterClientVersion.All']);
let SyncApiApproles = dynamic(['ADSynchronization.ReadWrite.All']);
let EntraConnectAppIdentities =
OAuthAppInfo
| where TimeGenerated >ago(7d)
| mv-expand Perm = parse_json(Permissions)
| where (Perm.TargetAppDisplayName == 'Microsoft Entra AD Synchronization Service' and Perm.PermissionValue has_any (SyncApiApproles))
or (Perm.TargetAppDisplayName == 'Microsoft password reset service' and Perm.PermissionValue has_any (PwdWriteBackAppRoles))
| summarize by ServicePrincipalId, OAuthAppId, AppName, AppOwnerTenantId, AppStatus, AddedOnTime, LastModifiedTime;
AADServicePrincipalSignInLogs
| where TimeGenerated > ago(1d)
| where ResultType == "0"
| where isnotempty(ServicePrincipalCredentialKeyId)
| where ServicePrincipalId in (EntraConnectAppIdentities | project ServicePrincipalId)
| extend Window = bin(TimeGenerated, window)
| sort by ServicePrincipalId asc, Window asc, TimeGenerated asc
| serialize
// prevKey only within same SP and window
| extend PrevKey = iff(prev(ServicePrincipalId) == ServicePrincipalId and prev(Window) == Window, prev(ServicePrincipalCredentialKeyId), ServicePrincipalCredentialKeyId)
| extend Switched = iif(ServicePrincipalCredentialKeyId != PrevKey, 1, 0)
| summarize
DistinctKeys = dcount(ServicePrincipalCredentialKeyId),
Switches = sum(Switched),
Events = count(),
ServicePrincipalCredentialKeys = make_set(tostring(ServicePrincipalCredentialKeyId)),
FirstSeen = min(TimeGenerated),
LastSeen = max(TimeGenerated)
by ServicePrincipalId, Window
// “Alternating” heuristic: exactly two keys present and at least 1 switches (e.g., A→B→A)
| where DistinctKeys >= 2 and Switches >= 1
| order by Switches desc, Events desc
